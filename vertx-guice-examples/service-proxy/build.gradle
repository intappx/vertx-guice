plugins {
    id 'java'
    id 'application'
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

def vertxVersion = "4.0.0";

mainClassName = 'com.intapp.vertx.guice.examples.serviceproxy.ServiceLauncher'

dependencies {
    compile 'com.google.inject:guice:4.2.3'
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-codegen:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"

    compile project(':vertx-guice')
}

/////////////////////////////////////////////////
// Code Gen support for service-proxy

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

task generateServiceProxy(type: JavaCompile, description: 'Generates EBServiceProxies') {
    source = sourceSets.main.java
    classpath = configurations.compile
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-AoutputDirectory=$destinationDir"
    ]
}

compileJava {
    source sourceSets.main.java + sourceSets.generated.java
}
compileJava.dependsOn generateServiceProxy

clean {
    delete += sourceSets.generated.java.srcDirs
}